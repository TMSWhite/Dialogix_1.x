options {
    LOOKAHEAD=1;
    STATIC=false;
}


PARSER_BEGIN(Qss)

import java.io.*;
import java.util.*;

public class Qss {
    /*static*/ java.util.Stack stack;
    /*static*/ Evidence data;
    /*static*/ Datum d = null;
    /*static*/ Vector errors = new Vector();


	public Datum parse(Evidence ev) {
		this.data = ev;

		try {
			stack = new java.util.Stack();

			this.Expressions();

			if (!stack.empty()) {
				d = (Datum) stack.pop();
			}
		}
		catch (java.util.EmptyStackException e) {
			errors.addElement("Stack underflow");
		}
		catch (ParseException e) {
			errors.addElement("Parse error: " + e.getMessage());
		}
		catch (TokenMgrError e) {
			errors.addElement("Tokenization error: " + e.getMessage());
		}
		finally {
			if (d == null)
				return new Datum(Datum.INVALID);
			else
				return d;
		}
	}

	public boolean hasErrors() {
		return (errors.size() > 0);
	}

	public Vector getErrors() {
		Vector temp = errors;
		errors = new Vector();
		return temp;
	}
}

PARSER_END(Qss)


TOKEN :
{
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            )
          )
      )*
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            )
          )
      )*
      "\""
  >
}

TOKEN : /* OPERATORS */
{
  		< PLUS: "+" >
  |	< MINUS: "-" >
  |	< MULTIPLY: "*" >
  |	< DIVIDE: "/" >
  |	< MODULUS: "%" >
  |	< AND: "&" >
  |	< OR: "|" >
  |	< XOR: "^" >
  |	< NOT: "!" >
  |	< GT: ">" >
  |	< GE: ">=" >
  |	< EQ: "==" >
  |	< NEQ: "!=" >
  |	< LT: "<" >
  |	< LE:	"<=" >
  |	< ANDAND: "&&" >
  |	< OROR: "||" >
  |	< LP: "(" >
  |	< RP: ")" >
  |	< COUNT: "count" >
  |	< LIST:	"list" >
  |	< DESC:	"desc" >
  |	< UNSET: "unset" >
  |	< DOT: "." >
  | < COMMA: "," >
  | < SEMICOLON: ";" >
}

SKIP :
{
  " "
| "\t"
| "\r"

}

TOKEN : /* numeric constants */
{
 <INTEGER_LITERAL: <DECIMAL_LITERAL> | <HEX_LITERAL> | <OCTAL_LITERAL> >
|  <#DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])*>
|  <#HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+>
|  <#OCTAL_LITERAL: "0" (["0"-"7"])*>
|  <FLOATING_POINT_LITERAL: (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])? | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])? | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])? | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]>
|  <#EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+>
}

TOKEN : /* Function names */
{
 	<  ID : ["a"-"z","A"-"Z", "_"] (["a"-"z","A"-"Z","0"-"9","_"])* >
 |	< EOL: "\n" >
}

void Expressions() : {}
{
	Expression() (LOOKAHEAD(2) (<COMMA> | <SEMICOLON> | <EOF>) { d = (Datum) stack.pop(); } Expression() )*
}

void Expression() : {}
{
	LOOKAHEAD(2) AssignmentExpression()
	| ConditionalExpression()
}

void AssignmentExpression(): { Token t; }
{
    (t = <ID>) "=" ConditionalExpression()
    	{
    		Datum d = (Datum) stack.pop();
    		data.set(t.image,d);
    		stack.push(d);
    	}
}

void ConditionalExpression() : {}
{
	LogicalORExpression() ("?" LogicalORExpression() ":" LogicalORExpression()
	{
		Datum c = (Datum) stack.pop();
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.conditional(a,b,c));
	}
	)*
}

void LogicalORExpression() : {}
{
	LogicalANDExpression()	( <OROR> LogicalANDExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.oror(a,b));
	}
	)*
}

void LogicalANDExpression() : {}
{
	InclusiveORExpression() ( <ANDAND> InclusiveORExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.andand(a,b));
	}
	)*
}

void InclusiveORExpression() : {}
{
	ExclusiveORExpression() ( <OR> ExclusiveORExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.or(a,b));
	}
	)*
}

void ExclusiveORExpression() : {}
{
	ANDExpression() ( <XOR> ANDExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.xor(a,b));
	}
	)*
}

void ANDExpression() : {}
{
	EqualityExpression() ( LOOKAHEAD(2) <AND> EqualityExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
		stack.push(DatumMath.and(a,b));
	}
	)*
}

void EqualityExpression() : { Token op; }
{
	RelationalExpression() ( ( <EQ> | <NEQ> ) { op = token; } RelationalExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
	    if ( op.kind == EQ ) {
			  stack.push(DatumMath.eq(a,b));
			}
	    else if (op.kind == NEQ) {
			  stack.push(DatumMath.neq(a,b));
		}
	}
	)*
}

void RelationalExpression() : { Token op; }
{
	AdditiveExpression() ( ( <LT> | <GT> | <LE> | <GE> ) { op = token; } AdditiveExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
	    switch(op.kind) {
	    	case LT: stack.push(DatumMath.lt(a,b)); break;
	    	case GT: stack.push(DatumMath.gt(a,b)); break;
	    	case LE: stack.push(DatumMath.le(a,b)); break;
	    	case GE: stack.push(DatumMath.ge(a,b)); break;
	   }
	}
	)*
}

void AdditiveExpression() : { Token op; }
{
	MultiplicativeExpression() ( LOOKAHEAD(2) ( <PLUS> | <MINUS> | <DOT> ) { op = token;} MultiplicativeExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
	    if ( op.kind == PLUS ) {
			  stack.push(DatumMath.add(a,b));
			}
	    else if (op.kind == MINUS) {
			stack.push(DatumMath.subtract(a,b));
		}
		else if (op.kind == DOT) {
			stack.push(DatumMath.concat(a,b));
		}
	}
	)*
}

void MultiplicativeExpression() : { Token op; }
{
	UnaryExpression() ( LOOKAHEAD(2) ( <MULTIPLY> | <DIVIDE> ) { op = token; } UnaryExpression()
	{
		Datum b = (Datum) stack.pop();
		Datum a = (Datum) stack.pop();
	    if ( op.kind == MULTIPLY ) {
			  stack.push(DatumMath.multiply(a,b));
			}
	    else if (op.kind == DIVIDE) {
	    	stack.push(DatumMath.divide(a,b));
		}
	}
	)*
}

void UnaryExpression() : { Token op; }
{
	(op = UnaryOperator()) PrimaryExpression()
	{
		switch(op.kind) {
			default:
				break;
			case MINUS: {
				stack.push(DatumMath.neg((Datum) stack.pop()));
	    		break;
	    	}
	    	case NOT: {
	    		stack.push(DatumMath.not((Datum) stack.pop()));
	    		break;
	    	}
	    }
	}
	| PrimaryExpression()
}

Token UnaryOperator() : {}
{
	( <PLUS> | <MINUS> | <NOT> ) { return token; }
}

void Count() : { long total; }
{
	<COUNT>
		<LP>
			(total = TruthTestList())
		<RP>
		{ stack.push(new Datum(total)); }
}

void StringList(): { java.util.Vector v = new java.util.Vector(); }
{
	<LIST> <LP>
		Expression() {v.addElement(stack.pop());}
		( "," (Expression()) {v.addElement(stack.pop());} )*
		<RP>
	{
		StringBuffer sb = new StringBuffer();

		/* Remove null strings from list */
		for (int i=0;i<v.size();) {
			if (((Datum) v.elementAt(i)).stringVal().length() == 0) {
				v.removeElementAt(i);
			}
			else {
				++i;
			}
		}

		for (int i=0;i<v.size();++i) {
			if (i > 0) {
				if (i < (v.size()-1)) {
					sb.append(", ");
				}
				else {
					sb.append(" and ");
				}
			}
			sb.append(((Datum) v.elementAt(i)).stringVal());
		}
		stack.push(new Datum(sb.toString(),Datum.STRING));
	}
}

void PrimaryExpression() : {}
{
	Constant() |
	Count() |
	StringList() |
	Description() |
	Unset() |
	Variable() |
	<LP> Expression() <RP>
}

long TruthTestList() : { long count=0; boolean b;}
{
	(b = TruthTest()) { if (b) ++count; } ( "," (b = TruthTest()) { if (b) ++count; })* { return count; }
}

boolean TruthTest() : {}
{
	Expression() {
		try {
			return ((Datum) stack.pop()).booleanVal();
		} catch (java.util.EmptyStackException e) {
			errors.addElement(e.getMessage());
		 }
		return false;
	}
}

void Constant() : { Token t; }
{
	((t = <INTEGER_LITERAL>) | (t = <FLOATING_POINT_LITERAL>))
	{
		stack.push(new Datum(t.image,Datum.DOUBLE));
	}
|	((t = <STRING_LITERAL>) | (t = <CHARACTER_LITERAL>))
	{
		// replace special characters before storing value
		StringBuffer sb = new StringBuffer();
		char c;

		try {
			for (int i=1;i<t.image.length()-1;++i) {
				c = t.image.charAt(i);
				if (c == '\\') {
					c = t.image.charAt(++i);
					switch (c) {
						case 'b': sb.append('\b'); break;
						case 'f': sb.append('\f'); break;
						case 'n': sb.append('\n'); break;
						case 'r': sb.append('\n'); break;
						case 't': sb.append('\t'); break;
						case '\'': sb.append('\''); break;
						case '\"': sb.append('\"'); break;
						case '\\': sb.append('\\'); break;
						default: sb.append(c); break;
					}
				}
				else {
					sb.append(c);
				}
			}
		}
		catch (IndexOutOfBoundsException e) {
			errors.addElement("Error parsing string '" + t.image + "': "  + e.getMessage());
		}

		stack.push(new Datum(sb.toString(),Datum.STRING));
	}

}

void Description() : { Token t; }
{
	<DESC> <LP> (t = <ID>) <RP>
	{
		if (data.containsKey(t.image)) {
			try {
				Node node = data.getNode(t.image);
				stack.push(new Datum(node.getDescription(),Datum.STRING));
			}
			catch (Exception e) {
				stack.push(new Datum(Datum.INVALID));
			}
		}
		else {
			stack.push(new Datum(Datum.INVALID));
		}
	}
}

void Unset() : { Token t; }
{
	<UNSET> <LP> (t = <ID>) <RP>
	{
		data.unset(t.image);
		stack.push(new Datum(Datum.INVALID));
	}
}

void Variable() : {}
{
	<ID>
	{
		if (data.containsKey(token.image)) {
			Datum d = data.getDatum(token.image);
			stack.push(d);
		}
		else {
			errors.addElement("Undefined variable '" + token.image + "'");
			stack.push(new Datum(Datum.INVALID));
		}
	}
}
